# Ship tree

# import tasks
import aim : "model.AI.task.Aim"
import shoot : "model.AI.task.Shoot"
import approach : "model.AI.task.Approach"
import fastDodge : "model.AI.task.FastDodge"
import flee : "model.AI.task.Flee"
import stepBack : "model.AI.task.StepBack"
import strafeDodge : "model.AI.task.StrafeDodge"
import forgetEnemy : "model.AI.task.ForgetEnemy"
import recordEnemy : "model.AI.task.RecordEnemy"

import isEnemyInSight : "model.AI.task.IsEnemyInSight"
import isEnemyTouching : "model.AI.task.IsEnemyTouching"
import isEnemyRecorded : "model.AI.task.IsEnemyRecorded"
import isEnemyDead : "model.AI.task.IsEnemyDead"
import isTooClose : "model.AI.task.IsTooClose"
import isTooFar : "model.AI.task.IsTooFar"
import isLowLife : "model.AI.task.IsLowLife"
import isEnemyAimed : "model.AI.task.IsEnemyAimed"


# import decorators
import cooldownTimer : "model.AI.decorator.CooldownTimer"
import persister : "model.AI.decorator.Persister"
import slower : "model.AI.decorator.Slower"
import succeeder : "model.AI.decorator.Succeeder"
import randomizer : "model.AI.decorator.Randomizer"


# tree
root
  selector
    sequence
      isEnemyRecorded
      selector
        sequence
          isEnemyDead
          forgetEnemy
        sequence
          isLowLife
          persister duration:3000 range:1000
            flee
          forgetEnemy
        sequence
          isTooClose dist:3
          persister duration:300 range:100
            flee
        sequence
          isTooClose dist:5
          aim
          sequence
            isEnemyAimed
            shoot
          stepBack
        sequence
          isTooFar dist:8
          aim
          sequence
            isEnemyAimed
            shoot
          approach
        sequence 
          isTooFar dist:10
          aim
          approach
        sequence 
          isTooFar dist:12
          forgetEnemy
        selector
          selector
            randomizer
              cooldownTimer duration:500 range:1000 id:"dodge"
                persister duration:200 range:200
                  sequence
                    aim
                    succeeder
                      sequence
                        isEnemyAimed
                        shoot
                    strafeDodge
            cooldownTimer duration:500 range:1000 id:"dodge"
              persister duration:200 range:200
                fastDodge
          aim
          sequence
            isEnemyAimed
            shoot
    sequence
      isEnemyInSight
      recordEnemy
    sequence
      isEnemyTouching
      recordEnemy
      